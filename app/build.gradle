buildscript {
  ext.ktor_version = '1.2.2'
  ext.kotlin_version_for_app = '1.3.41'

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version_for_app"
  }
}

plugins {
  id "application"
  id "com.github.johnrengelman.shadow" version "5.1.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'

java {
  disableAutoTargetJvm()
}

group 'corda-and-ktor'
version '1.0.0'

mainClassName = "dev.lankydan.tutorial.server.ServerKt"

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url 'https://software.r3.com/artifactory/corda-releases' }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version_for_app"
  testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version_for_app"
  testCompile "junit:junit:$junit_version"

  compile "$corda_release_group:corda-jackson:$corda_release_version"
  compile "$corda_release_group:corda-rpc:$corda_release_version"
  compile "$corda_release_group:corda:$corda_release_version"
  compile "io.ktor:ktor-server-netty:$ktor_version"
  compile "ch.qos.logback:logback-classic:1.2.3"
  implementation ("io.ktor:ktor-jackson:$ktor_version") {
    exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-kotlin'
  }
  compile project(":contracts")
  compile project(":workflows")
}

task runPartyAServer(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'dev.lankydan.tutorial.server.ServerKt'
  systemProperty "server.port", "10011"
  systemProperty "config.rpc.username", "user1"
  systemProperty "config.rpc.password", "test"
  systemProperty "config.rpc.host", "localhost"
  systemProperty "config.rpc.port", "10006"
}

task runPartyBServer(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'dev.lankydan.tutorial.server.ServerKt'
  systemProperty "server.port", "10013"
  systemProperty "config.rpc.username", "user1"
  systemProperty "config.rpc.password", "test"
  systemProperty "config.rpc.host", "localhost"
  systemProperty "config.rpc.port", "10009"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    languageVersion = "1.3"
    apiVersion = "1.3"
    jvmTarget = "1.8"
    javaParameters = true // Useful for reflection.
  }
}